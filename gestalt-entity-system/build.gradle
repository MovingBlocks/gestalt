// Copyright 2021 The Terasology Foundation
// SPDX-License-Identifier: Apache-2.0

/*
 * This is a Gradle build file:
 * - Gradle Homepage: http://gradle.org/
 * - Gradle Documentation: http://gradle.org/documentation
 * - View tasks for this project: $ gradlew tasks
 */

apply plugin: 'java-library'
apply plugin: 'maven-publish'

// Primary dependencies definition
dependencies {
    implementation project(":gestalt-util")
    implementation project(":gestalt-module")
    implementation project(":gestalt-asset-core")

    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "com.android.support:support-annotations:$android_annotation_version"
    implementation "net.jcip:jcip-annotations:$jcip_annotation_version"
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation "com.google.code.gson:gson:$gson_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "ch.qos.logback:logback-classic:$logback_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.8"
    }
}

description = 'A threadsafe entity system implementation. ' +
        'An entity system is a component based data model.'

sourceCompatibility = 1.8
targetCompatibility = 1.8


/***
 * Publishing
 ***/

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            credentials {
                username project.gestaltMavenRepoUsername
                password project.gestaltMavenRepoPassword
            }
            name 'TerasologyTestRepo'
            allowInsecureProtocol = true  // ðŸ˜±
            url project.version.endsWith('SNAPSHOT') ? project.snapshotPublishRepo : project.releasePublishRepo
        }
    }
    publications {
        mavenEs(MavenPublication) {
            artifactId = project.name
            version = project.version

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

