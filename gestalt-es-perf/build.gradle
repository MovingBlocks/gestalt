// Copyright 2021 The Terasology Foundation
// SPDX-License-Identifier: Apache-2.0

/*
 * This is a Gradle build file:
 * - Gradle Homepage: http://gradle.org/
 * - Gradle Documentation: http://gradle.org/documentation
 * - View tasks for this project: $ gradlew tasks
 */

apply plugin: 'java-library'
apply plugin: 'maven-publish'

// Primary dependencies definition
dependencies {
    implementation project(":gestalt-util")
    implementation project(":gestalt-module")
    implementation project(":gestalt-asset-core")
	implementation project(":gestalt-entity-system")

    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "com.android.support:support-annotations:$android_annotation_version"
    implementation "net.jcip:jcip-annotations:$jcip_annotation_version"
    
    testImplementation "ch.qos.logback:logback-classic:$logback_version"
    testImplementation(platform("org.junit:junit-bom:$jupiter_version")) {
        // junit-bom will set version numbers for the other org.junit dependencies.
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.mockito:mockito-inline:$mockito_version")
    testImplementation("org.mockito:mockito-junit-jupiter:$mockito_version")
}

description = 'High performance access methods to replace the use of reflections in gestalt-entity-system. Can be used in Java 7+ and Android API 26+.'

sourceCompatibility = 1.8
targetCompatibility = 1.8

/***
 * Testpack inclusion
 */

task gatherJarModules(dependsOn: [':testpack:moduleF:jar'], type: Copy)
task gatherModules(dependsOn: [':gestalt-es-perf:gatherJarModules'])

gatherJarModules {
    from '../testpack/moduleF/build/libs/'
    into 'test-modules'
    include('*.jar')
}

tasks.named("test", Test) {
    dependsOn(gatherModules)
    useJUnitPlatform()
}

/***
 * Publishing
 ***/

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            credentials {
                username project.gestaltMavenRepoUsername
                password project.gestaltMavenRepoPassword
            }
            name 'TerasologyTestRepo'
            allowInsecureProtocol = true  // ðŸ˜±
            url project.version.endsWith('SNAPSHOT') ? project.snapshotPublishRepo : project.releasePublishRepo
        }
    }
    publications {
        mavenEsPerf(MavenPublication) {
            artifactId = project.name
            version = project.version

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}


