/*
 * Copyright 2019 MovingBlocks
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 * This is a Gradle build file:
 * - Gradle Homepage: http://gradle.org/
 * - Gradle Documentation: http://gradle.org/documentation
 * - View tasks for this project: $ gradlew tasks
 */

apply plugin: 'java-library'
apply plugin: 'maven-publish'

repositories {
    maven {
        url = 'https://heisluft.tk/maven/'
    }
}

// Primary dependencies definition
dependencies {
    api 'org.terasology:reflections:0.9.12-MB'

    implementation project(":gestalt-util")
    implementation "com.google.guava:guava:$guava_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation 'org.apache.commons:commons-vfs2:2.2'
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "com.android.support:support-annotations:$android_annotation_version"
    implementation "com.github.zafarkhaja:java-semver:0.10.0"

    testImplementation project(":testpack:testpack-api")
    testImplementation "junit:junit:$junit_version"
    testImplementation "ch.qos.logback:logback-classic:$logback_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"

}

// Library and distribution config
description = 'Provides support for modules - java libraries that can be activated at runtime and run in a sandboxed environment'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task gatherJarModules(dependsOn: [':testpack:moduleA:jar', ':testpack:moduleB:jar', ':testpack:moduleC:jar', ':testpack:moduleD:jar'], type: Copy)
task createModuleE(dependsOn: [':gestalt-module:copyModuleEInfo', ':gestalt-module:copyModuleELibs'])
task copyModuleELibs(dependsOn: [':testpack:moduleA:jar', ':testpack:moduleD:jar'], type: Copy)
task copyModuleEInfo(type: Copy)
task gatherModules(dependsOn: [':gestalt-module:gatherJarModules', ':gestalt-module:createModuleE'])

copyModuleELibs {
    from '../testpack/moduleA/build/libs'
    from '../testpack/moduleD/build/libs'
    into 'test-modules/moduleE/libs'
    include('*.jar')
}

copyModuleEInfo {
    from '../testpack/moduleE'
    into 'test-modules/moduleE'
    include('*.json')
}

gatherJarModules {
    from '../testpack/moduleA/build/libs/'
    from '../testpack/moduleB/build/libs/'
    from '../testpack/moduleC/build/libs/'
    from '../testpack/moduleD/build/libs/'
    into 'test-modules'
    include('*.jar')
}

test.dependsOn gatherModules

/***
 * Publishing
 ***/

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            credentials {
                username project.gestaltMavenRepoUsername
                password project.gestaltMavenRepoPassword
            }
            name 'TerasologyTestRepo'
            url project.version.endsWith('SNAPSHOT') ? project.snapshotPublishRepo : project.releasePublishRepo
        }
    }
    publications {
        mavenModule(MavenPublication) {
            artifactId = project.name
            version = project.version

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

