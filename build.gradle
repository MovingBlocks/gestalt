// Copyright 2021 The Terasology Foundation
// SPDX-License-Identifier: Apache-2.0
buildscript {
    repositories {
        google()
        mavenCentral()
        // org.jetbrains.trove4j:trove4j
        gradlePluginPortal()
        maven {
            url 'https://heisluft.de/maven'
        }
        maven {
            name = "Terasology Artifactory"
            url = "http://artifactory.terasology.org/artifactory/libs-release-local"
            allowInsecureProtocol = true  // ðŸ˜±
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.1'
    }
}

ext {
    // Android version support
    android_annotation_version = "28.0.0"

    // Standard Utility
    guava_version = "27.0.1-android"
    slf4j_version = "1.7.25"
    gson_version = "2.8.5"
    jcip_annotation_version = "1.0"

    // Testing
    junit_version = "4.13.2"
    jupiter_version = "5.7.1"
    logback_version = "1.2.3"
    mockito_version = "3.7.7"
}

allprojects {
    repositories {
        google()
        mavenCentral()

        // Terasology Artifactory instance for libs not readily available elsewhere plus our own libs
        maven {
            def repoViaEnv = System.getenv()["RESOLUTION_REPO"]
            if (rootProject.hasProperty("alternativeResolutionRepo")) {
                // If the user supplies an alternative repo via gradle.properties then use that
                name "from alternativeResolutionRepo property"
                url alternativeResolutionRepo
            } else if (repoViaEnv != null && repoViaEnv != "") {
                name "from \$RESOLUTION_REPO"
                url = repoViaEnv
            } else {
                // Our default is the main virtual repo containing everything except repos for testing Artifactory itself
                name "Terasology Artifactory"
                url "http://artifactory.terasology.org/artifactory/virtual-repo-live"
                allowInsecureProtocol true  // ðŸ˜±
            }
        }

        // SemVer lib
        maven {
            url 'https://heisluft.de/maven'
        }
    }
}

// Library and distribution config
description = 'A set of libraries providing core capabilities for games and game engines'

task clean(type: Delete) {
    delete rootProject.buildDir
}
