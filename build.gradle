// Copyright 2021 The Terasology Foundation
// SPDX-License-Identifier: Apache-2.0

// Only needed for the two Android modules as of April 2021
buildscript {
    repositories {
        mavenCentral()

        // Needed for the Android Gradle Plugin to work
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.1'
    }
}

plugins {
    id 'idea'
}

ext {
    // Android version support
    android_annotation_version = "28.0.0"

    // Standard Utility
    guava_version = "27.0.1-android"
    slf4j_version = "1.7.25"
    gson_version = "2.8.5"
    jcip_annotation_version = "1.0"
    joml_version = "1.10.0"
    lwjgl_version = '3.2.3'

    // Testing
    junit_version = "4.12"
    logback_version = "1.2.3"
    mockito_version = "1.10.19"
}

allprojects {
    repositories {
        google()
        mavenCentral()

        // Terasology Artifactory instance for libs not readily available elsewhere plus our own libs
        maven {
            def repoViaEnv = System.getenv()["RESOLUTION_REPO"]
            if (rootProject.hasProperty("alternativeResolutionRepo")) {
                // If the user supplies an alternative repo via gradle.properties then use that
                name "from alternativeResolutionRepo property"
                url alternativeResolutionRepo
            } else if (repoViaEnv != null && repoViaEnv != "") {
                name "from \$RESOLUTION_REPO"
                url = repoViaEnv
            } else {
                // Our default is the main virtual repo containing everything except repos for testing Artifactory itself
                name "Terasology Artifactory"
                url "http://artifactory.terasology.org/artifactory/virtual-repo-live"
                allowInsecureProtocol true  // ðŸ˜±
            }
        }

        // SemVer lib
        maven {
            url 'https://heisluft.de/maven'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
